# Copyright 2025 The Pigweed Authors
#
# Licensed under the Apache License, Version 2.0 (the "License"); you may not
# use this file except in compliance with the License. You may obtain a copy of
# the License at
#
#     https://www.apache.org/licenses/LICENSE-2.0
#
# Unless required by applicable law or agreed to in writing, software
# distributed under the License is distributed on an "AS IS" BASIS, WITHOUT
# WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied. See the
# License for the specific language governing permissions and limitations under
# the License.

load("@rules_rust//rust:defs.bzl", "rust_binary", "rust_library")
load("//pw_build:merge_flags.bzl", "flags_from_dict")
load("//pw_kernel:flags.bzl", "KERNEL_DEVICE_COMMON_FLAGS")
load("//pw_kernel/tooling:system_image.bzl", "system_image", "system_image_test")
load("//pw_kernel/tooling:target_codegen.bzl", "target_codegen")
load("//pw_kernel/tooling:target_linker_script.bzl", "target_linker_script")
load(":defs.bzl", "TARGET_COMPATIBLE_WITH")

package(default_visibility = ["//visibility:public"])

# Custom constraint for this target
constraint_value(
    name = "target_armv7m_minimal",
    constraint_setting = "//pw_kernel/target:target",
)

# Platform definition with kernel config flag
platform(
    name = "armv7m_minimal",
    constraint_values = [
        ":target_armv7m_minimal",
        "//pw_build/constraints/arm:cortex-m3",
        "@platforms//cpu:armv7-m",
        "@platforms//os:none",
        "//pw_build/constraints/rust:no_std",
    ],
    flags = flags_from_dict(
        KERNEL_DEVICE_COMMON_FLAGS | {
            "//pw_kernel/config:kernel_config": ":config",
            "//pw_kernel/subsys/console:console_backend": "//pw_kernel/subsys/console:console_backend_semihosting",
        },
    ),
)

# Static kernel configuration
rust_library(
    name = "entry",
    srcs = ["entry.rs"],
    edition = "2024",
    tags = ["kernel"],
    target_compatible_with = TARGET_COMPATIBLE_WITH,
    deps = [
        "//pw_kernel/arch/arm_cortex_m:arch_arm_cortex_m",
        "//pw_kernel/kernel",
        "@rust_crates//:cortex-m-rt",
    ],
)

rust_library(
    name = "config",
    srcs = ["config.rs"],
    crate_name = "kernel_config",
    edition = "2024",
    deps = ["//pw_kernel/config:kernel_config_interface"],
)

# System configuration file
filegroup(
    name = "system_config",
    srcs = ["system.json5"],
)

# Generate kernel configuration code from system.json5
target_codegen(
    name = "codegen",
    arch = "//pw_kernel/arch/arm_cortex_m:arch_arm_cortex_m",
    system_config = ":system_config",
    target_compatible_with = TARGET_COMPATIBLE_WITH,
)

# Linker script template for this target
filegroup(
    name = "linker_script_template",
    srcs = ["target.ld.jinja"],
)

# Generate linker script for this target
target_linker_script(
    name = "linker_script",
    system_config = ":system_config",
    tags = ["kernel"],
    target_compatible_with = TARGET_COMPATIBLE_WITH,
    template = ":linker_script_template",
)

# Kernel binary (using generated config)
rust_binary(
    name = "kernel_target",
    srcs = [
        "target.rs",
    ],
    edition = "2024",
    tags = ["kernel"],
    target_compatible_with = TARGET_COMPATIBLE_WITH,
    deps = [
        ":codegen",
        ":entry",
        ":linker_script",
        "//pw_kernel/arch/arm_cortex_m:arch_arm_cortex_m",
        "//pw_kernel/kernel",
        "//pw_kernel/subsys/console:console_backend",
        "//pw_kernel/target:target_common",
        "//pw_kernel/userspace",
        "@rust_crates//:cortex-m-semihosting",
    ],
)

# Complete system image (kernel + all processes)
system_image(
    name = "minimal",
    apps = [
        "//pw_kernel/target/armv7m_minimal/hello",
    ],
    kernel = ":kernel_target",
    platform = ":armv7m_minimal",  # Use our custom platform
    system_config = ":system_config",
    tags = ["kernel"],
)

# Executable wrapper with target compatibility
system_image_test(
    name = "minimal_test",
    image = ":minimal",
    target_compatible_with = TARGET_COMPATIBLE_WITH,
)
